buildscript {
    ext {
        springBootVersion = '3.1.7'
        //oracleVersion = '11.2.0.4'
        notifyVersion = '3.17.3-RELEASE'
        springDocVersion = '2.2.0'
        queryDslVersion = '5.0.0'
        flywayVersion = '10.4.1'
        postgresVersion = '42.7.1'
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.postgresql:postgresql:${postgresVersion}")
        classpath("org.flywaydb:flyway-database-postgresql:${flywayVersion}")
    }
}

plugins {
    id 'org.springframework.boot' version "${springBootVersion}"
    id "org.flywaydb.flyway" version "${flywayVersion}"
    id "com.dorongold.task-tree" version "1.3.1"
    id 'org.cyclonedx.bom' version '1.4.1'
    id 'org.owasp.dependencycheck' version '8.4.0'
    id 'checkstyle'
    id 'pmd'
    id 'com.github.ben-manes.versions' version '0.47.0'
    id 'org.sonarqube' version '4.4.1.3373'

//      id 'com.ewerk.gradle.plugins.querydsl' version '1.0.10'
//      id 'io.franzbecker.gradle-lombok' version '5.0.0'
}
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'io.spring.dependency-management'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" //<< "-Werror"
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
    javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

bootJar {
    archiveFileName = "juror-api.jar"
    manifest {
        attributes('Implementation-Version': project.version.toString())
    }
}

jacoco {
    toolVersion = "0.8.11"
}

version = '0.0.1'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

configurations {
    dev
    integrationImplementation.extendsFrom testImplementation
    integrationRuntimeOnly.extendsFrom testRuntime
    compileOnly {
        extendsFrom annotationProcessor
    }
    querydsl.extendsFrom compileClasspath
}

dependencies {

    //Internal dependency overrides
    implementation 'org.yaml:snakeyaml:2.2'
    implementation 'org.apache.tomcat.embed:tomcat-embed-websocket:10.1.13'
    implementation 'org.apache.tomcat.embed:tomcat-embed-core:10.1.13'
    implementation 'org.json:json:20230618'
    //
    implementation('org.springframework.boot:spring-boot-starter-actuator')
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    implementation("org.springframework.data:spring-data-envers:${springBootVersion}")
    implementation('org.springframework.boot:spring-boot-starter-security')
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-json')
    //implementation("com.oracle:ojdbc6:${oracleVersion}")
    //implementation("com.oracle.database.jdbc:ojdbc11:23.2.0.0")
    implementation group: 'org.postgresql', name: 'postgresql', version: "${postgresVersion}"
    runtimeOnly "org.flywaydb:flyway-database-postgresql:${flywayVersion}"
    runtimeOnly "org.flywaydb:flyway-core:${flywayVersion}"

    implementation 'io.jsonwebtoken:jjwt-api:0.12.2'
    implementation 'javax.mail:mail:1.4.7'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.2'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.2'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.2.1'

    implementation('com.google.guava:guava:31.1-jre')

    implementation('org.hibernate.orm:hibernate-core:6.3.0.CR1')

    // QueryDSL
    implementation("com.querydsl:querydsl-core:${queryDslVersion}")
    implementation("com.querydsl:querydsl-jpa:${queryDslVersion}:jakarta")

    testImplementation 'junit:junit:4.13.2'
    testRuntimeOnly("org.junit.vintage:junit-vintage-engine:5.10.0")
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'

    annotationProcessor("com.querydsl:querydsl-apt:${queryDslVersion}:jakarta")
    annotationProcessor('jakarta.persistence:jakarta.persistence-api:3.1.0')

    implementation('org.projectlombok:lombok:1.18.28')
    annotationProcessor('org.projectlombok:lombok')

    implementation('org.apache.commons:commons-lang3:3.12.0')
    implementation('org.apache.commons:commons-text:1.10.0')
    implementation("uk.gov.service.notify:notifications-java-client:${notifyVersion}")

    runtimeOnly('org.springframework.boot:spring-boot-devtools')

    implementation 'org.springframework.boot:spring-boot-starter-validation'

    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springDocVersion}"


    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('org.springframework.boot:spring-boot-starter-data-jpa')

    testImplementation 'org.apache.httpcomponents.client5:httpclient5:5.2.1'
    //testImplementation 'org.apache.httpcomponents:httpclient:4.5.13'
    testImplementation 'net.javacrumbs.json-unit:json-unit-fluent:2.36.0'
    testImplementation('org.assertj:assertj-core:3.23.1')
    testImplementation 'org.mockito:mockito-core:5.6.0'

    testImplementation('org.ajbrown:name-machine:1.0.0')
    testImplementation('org.projectlombok:lombok')
    testAnnotationProcessor('org.projectlombok:lombok')

    testImplementation('net.bytebuddy:byte-buddy:1.12.19')
    testImplementation('junit:junit:4.13.2')

    // Validation
    testImplementation 'org.wiremock:wiremock:3.2.0'
}
//
//task deleteQueryDslClasses(type: Delete) {
//    delete "src/querydsl/java"
//}
//
//task generateQueryDSL(type: JavaCompile) {
//    source = sourceSets.main.java.srcDirs
//    classpath = sourceSets.main.compileClasspath
//    getDestinationDirectory().set(file("src/querydsl/java"))
//    options.annotationProcessorPath = configurations.annotationProcessor
//    options.compilerArgs = [
//            '-proc:only',
//            '-processor',
//            'com.querydsl.apt.jpa.JPAAnnotationProcessor',
//            '-Aquerydsl.generatedAnnotationClass=com.querydsl.core.annotations.Generated'
//    ]
//}

//querydsl {
//    hibernate = true
//    jpa = true
//}
//
//compileQuerydsl {
//    options.annotationProcessorPath = configurations.querydsl
//}

jacocoTestReport {
    reports {
        xml.setRequired(true)
        csv.setRequired(false)
        html.getOutputLocation().set(file("${buildDir}/jacocoHtml"))
    }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport

sonarqube {
    properties {
        property "sonar.projectName", "Juror :: juror-api"
        property "sonar.projectKey", "uk.gov.hmcts.juror:juror-api"
        property "sonar.sources", "src"
        property "sonar.java.binaries", "build/classes"
        property "sonar.exclusions", "**/*, src/**/*"
        property "sonar.cpd.exclusions", "**/*, src/**/*"
        // property "sonar.exclusions", ".github/**,charts/**/,config/**,infrastructure/**,lib/**,gradle/**,data/*,src/querydsl/**,src/test/**,src/integration-test/**,src/main/resources/**"
        // property "sonar.cpd.exclusions", "src/main/resources/db/migration/*,src/main/java/uk/gov/hmcts/juror/api/moj/xerox/letters/*"
    }
}

bootRun {
    args = ["--spring.profiles.active=development"]
    classpath += configurations.dev
}
test {
    useJUnitPlatform()
}

tasks.withType(Test).configureEach() {

    dependsOn compileJava
    if (!System.getenv('JENKINS_CI_BUILD')) {
        println('NOT running in CI mode! (JENKINS_CI_BUILD env variable not found).')
        //systemProperty "spring.profiles.active", "development"
        systemProperty "spring.profiles.active", "development,test"
    } else {
        println('Running in CI mode! (JENKINS_CI_BUILD env variable present).')
        systemProperty "spring.profiles.active", project.property('springProfile') + ',test'
        testLogging {
            events "failed", "standard_out"
            exceptionFormat "full"
        }
    }

    jacoco {
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
    }

    finalizedBy(jacocoTestReport)
}

// Flyway must align with application.yml
flyway {
    url = System.getenv("DB_URL")
    user = System.getenv("DB_USERNAME")
    password = System.getenv("DB_PASSWORD")
    schemas = ['juror','juror_digital','juror_digital_user','juror_dashboard','hk','juror_mod']
    defaultSchema = 'juror_mod'
    table = 'schema_history'
    cleanDisabled = false
}

springBoot {
    buildInfo {
        properties {
            additional = [
                    'phase': 'Beta Public'
            ]
        }
    }
}

task copyDocs(type: Copy, group: 'documentation') {
    from "${project.rootDir}/src/docs"
    into "${project.buildDir}/docs"
}
processResources.finalizedBy(copyDocs)


wrapper {
    gradleVersion = '8.3'
}

cyclonedxBom {

    // Currently using v 1.4.1 , so many config items are not supported, but retained below for future upgrades

    // includeConfigs is the list of configuration names to include when generating the BOM (leave empty to include every configuration)
    //includeConfigs = ["runtimeClasspath"]
    includeConfigs = ["runtimeClasspath"]
    // skipConfigs is a list of configuration names to exclude when generating the BOM
    skipConfigs += ["/src/test", "src/integration-test"]
    // skipProjects is a list of project names to exclude when generating the BOM
    //skipProjects = [rootProject.name, "yourTestSubProject"]
    // Specified the type of project being built. Defaults to 'library'
    //projectType = "application"
    // Specified the version of the CycloneDX specification to use. Defaults to 1.4.
    //schemaVersion = "1.2"
    // Boms destination directory (defaults to build/reports)
    // destination = file("build/reports")
    // The file name for the generated BOMs (before the file format suffix). Defaults to 'bom'
    //outputName = "bom"
    // The file format generated, can be xml, json or all for generating both
    //outputFormat = "json"
    // Exclude BOM Serial Number
    //includeBomSerialNumber = false
    // Override component version
    //componentVersion = "2.0.0"
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/java'
            //srcDirs 'src/querydsl/java'
        }
    }
    integration {
        java {
            compileClasspath += main.output + test.output + test.compileClasspath
            runtimeClasspath += main.output + test.output + test.runtimeClasspath
            annotationProcessorPath += main.output + test.output + main.annotationProcessorPath + test.annotationProcessorPath
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

checkstyleMain {
    maxWarnings = 398
}
checkstyleTest {
    maxWarnings = 271
}
checkstyleIntegration{
    maxWarnings = 1272
}
checkstyle {
    maxWarnings = 0
    toolVersion = '10.12.3'
    getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

pmdIntegration {
    maxFailures = 809
}
pmdTest {
    maxFailures = 661
}
pmdMain {
    maxFailures = 1283
}
pmd {
    maxFailures = 0
    toolVersion = "6.55.0"
    sourceSets = [sourceSets.main, sourceSets.test, sourceSets.integration
    ]
    reportsDir = file("$project.buildDir/reports/pmd")
    // https://github.com/pmd/pmd/issues/876
    ruleSets = []
    ruleSetFiles = files("config/pmd/ruleset.xml")
}

test {
    if(System.getenv('JENKINS_CI_BUILD')){
        failFast = true
    }
}

task integration(type: Test) {
    useJUnitPlatform()
    description = 'Runs integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath
    // This forces integration tests to always run if this task is run.
    outputs.upToDateWhen { false }
    if(System.getenv('JENKINS_CI_BUILD')){
        failFast = true
    }
}

task testSuite(dependsOn: [test, integration]) {
    description = 'Runs both unit tests and integration tests.'
    group = 'verification'

    // This forces the test suite to always run if this task is run.
    outputs.upToDateWhen { false }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 0-10 fails the build, anything greater and it doesn't fail the build
    failBuildOnCVSS = 10
    suppressionFile = 'config/owasp/suppressions.xml'

    analyzers {
        // Disable scanning of .NET related binaries
        assemblyEnabled = false
    }
    skipConfigurations = [
            "checkstyle",
            "compileOnly",
            "pmd",
            "integrationTest",
            "functionalTest",
            "test",
            "smokeTest",
            "contractTestRuntimeClasspath",
            "contractTestCompileClasspath"
    ]
}

// Makes integration tests not run by default.
check.dependsOn -= integration
// Makes the whole test suite not run by default.
check.dependsOn -= testSuite

task runAllStyleChecks {
    dependsOn 'checkstyleMain'
    dependsOn 'checkstyleTest'
    dependsOn 'checkstyleIntegration'

    dependsOn 'pmdMain'
    dependsOn 'pmdTest'
    dependsOn 'pmdIntegration'
}

buildscript {
    ext {
        springBootVersion = '3.2.4'
        notifyVersion = '5.0.0-RELEASE'
        springDocVersion = '2.4.0'
        queryDslVersion = '5.1.0'
        flywayVersion = '10.10.0'
        postgresVersion = '42.7.3'
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.postgresql:postgresql:${postgresVersion}")
        classpath("org.flywaydb:flyway-database-postgresql:${flywayVersion}")
    }
}

plugins {
    id 'org.springframework.boot' version "${springBootVersion}"
    id "org.flywaydb.flyway" version "${flywayVersion}"
    id "com.dorongold.task-tree" version "2.1.1"
    id 'org.owasp.dependencycheck' version '9.0.10'
    id 'checkstyle'
    id 'pmd'
    id 'com.github.ben-manes.versions' version '0.51.0'
    id 'org.sonarqube' version '4.4.1.3373'
}
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'io.spring.dependency-management'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" //<< "-Werror"
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
    javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

bootJar {
    archiveFileName = "juror-api.jar"
}

jacoco {
    toolVersion = "0.8.11"
}

version = '0.0.1'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

configurations {
    dev
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly

    functionalTestImplementation.extendsFrom testImplementation
    functionalTestRuntimeOnly.extendsFrom runtimeOnly

    smokeTestImplementation.extendsFrom testImplementation
    smokeTestRuntimeOnly.extendsFrom runtimeOnly

    compileOnly {
        extendsFrom annotationProcessor
    }
    querydsl.extendsFrom compileClasspath
}

dependencies {

    //Internal dependency overrides
    implementation 'org.yaml:snakeyaml:2.2'
    implementation 'org.apache.tomcat.embed:tomcat-embed-websocket:10.1.20'
    implementation 'org.apache.tomcat.embed:tomcat-embed-core:10.1.20'
    //
    implementation("org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}")
    implementation("org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}")
    implementation("org.springframework.data:spring-data-envers:${springBootVersion}")
    implementation("org.springframework.boot:spring-boot-starter-security:${springBootVersion}")
    implementation("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
    implementation("org.springframework.boot:spring-boot-starter-json:${springBootVersion}")
    implementation("org.springframework.boot:spring-boot-starter-validation:${springBootVersion}")
    runtimeOnly("org.springframework.boot:spring-boot-devtools:${springBootVersion}")


    implementation group: 'org.postgresql', name: 'postgresql', version: "${postgresVersion}"
    implementation "org.flywaydb:flyway-database-postgresql:${flywayVersion}"
    implementation "org.flywaydb:flyway-core:${flywayVersion}"

    implementation 'io.jsonwebtoken:jjwt-api:0.12.5'
    implementation 'javax.mail:mail:1.4.7'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.5'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.3.1'

    implementation('com.google.guava:guava:33.1.0-jre')

    implementation('org.hibernate.orm:hibernate-core:6.3.0.CR1')

    // QueryDSL
    implementation("com.querydsl:querydsl-core:${queryDslVersion}")
    implementation("com.querydsl:querydsl-jpa:${queryDslVersion}:jakarta")

    testImplementation 'junit:junit:4.13.2'
    testRuntimeOnly("org.junit.vintage:junit-vintage-engine:5.10.2")
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'

    annotationProcessor("com.querydsl:querydsl-apt:${queryDslVersion}:jakarta")
    annotationProcessor('jakarta.persistence:jakarta.persistence-api:3.1.0')

    implementation('org.projectlombok:lombok:1.18.32')
    annotationProcessor('org.projectlombok:lombok')

    implementation('org.apache.commons:commons-lang3:3.14.0')
    implementation('org.apache.commons:commons-text:1.11.0')
    implementation("uk.gov.service.notify:notifications-java-client:${notifyVersion}")

    runtimeOnly(group: 'com.microsoft.azure', name: 'applicationinsights-core', version: '3.5.1')

    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springDocVersion}"
    implementation group: 'io.rest-assured', name: 'rest-assured'

    testImplementation("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
    testImplementation("org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}")

    testImplementation 'org.apache.httpcomponents.client5:httpclient5:5.3.1'
    //testImplementation 'org.apache.httpcomponents:httpclient:4.5.13'
    testImplementation 'net.javacrumbs.json-unit:json-unit-fluent:3.2.7'
    testImplementation('org.assertj:assertj-core:3.25.3')
    testImplementation 'org.mockito:mockito-core:5.11.0'

    testImplementation('org.projectlombok:lombok')
    testAnnotationProcessor('org.projectlombok:lombok')

    testImplementation('net.bytebuddy:byte-buddy:1.14.12')
    testImplementation('junit:junit:4.13.2')
    testImplementation "org.testcontainers:testcontainers:1.19.7"
    testImplementation "org.testcontainers:junit-jupiter:1.19.7"
    testImplementation 'org.testcontainers:postgresql:1.19.7'

    // Validation
    testImplementation 'org.wiremock:wiremock-standalone:3.4.2'
}

sonarqube {
    properties {
        property "sonar.projectName", "Juror :: juror-api"
        property "sonar.projectKey", "uk.gov.hmcts.juror:juror-api"
        property "sonar.sources", "src"
        property "sonar.java.binaries", "build/classes"
        property "sonar.exclusions", "**/*, src/**/*"
        property "sonar.test.exclusions", "**/*, src/test/**/*"
        property "sonar.cpd.exclusions", "**/*, src/**/*"
        // property "sonar.exclusions", ".github/**,charts/**/,config/**,infrastructure/**,lib/**,gradle/**,data/*,src/querydsl/**,src/test/**,src/integration-test/**,src/main/resources/**"
        // property "sonar.cpd.exclusions", "src/main/resources/db/migration/*,src/main/java/uk/gov/hmcts/juror/api/moj/xerox/letters/*"
    }
}

bootRun {
    args = ["--spring.profiles.active=development"]
    classpath += configurations.dev
}
test {
    useJUnitPlatform()
}

tasks.withType(Test).configureEach() {

    dependsOn compileJava
    if (!System.getenv('BUILD_ID')) {
        println('NOT running in CI mode! (BUILD_ID env variable not found).')
        systemProperty "spring.profiles.active", "development,test"
    } else {
        println('Running in CI mode! (BUILD_ID env variable present).')
        systemProperty "spring.profiles.active", "development,test"
        testLogging {
            events "failed", "standard_out"
            exceptionFormat "full"
        }
    }

    jacoco {
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
    }

    finalizedBy(jacocoTestReport)
}

// Flyway must align with application.yml
flyway {
    def dbHost = System.getenv("DB_HOST") ?: "localhost"
    def dbPort = System.getenv("DB_PORT") ?: "5432"
    def dbName = System.getenv("DB_NAME") ?: "juror"
    def dbUserName = System.getenv("DB_USER_NAME") ?: "system"
    def dbPassword = System.getenv("DB_PASSWORD") ?: "postgres"

    url = "jdbc:postgresql://$dbHost:$dbPort/$dbName"
    user = "$dbUserName"
    password = "$dbPassword"
    schemas = ['juror', 'juror_digital', 'juror_digital_user', 'juror_dashboard', 'hk', 'juror_mod']
    defaultSchema = 'juror_mod'
    table = 'schema_history'
    cleanDisabled = false
}

springBoot {
    buildInfo {
        properties {
            additional = [
                    'phase': 'Beta Public'
            ]
        }
    }
}

task copyDocs(type: Copy, group: 'documentation') {
    from "${project.rootDir}/src/docs"
    into "${project.buildDir}/docs"
}
processResources.finalizedBy(copyDocs)


wrapper {
    gradleVersion = '8.7'
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/java'
            //srcDirs 'src/querydsl/java'
        }
    }
    functionalTest {
        java {
            compileClasspath += main.output + test.output + test.compileClasspath
            runtimeClasspath += main.output + test.output + test.runtimeClasspath
            annotationProcessorPath += main.output + test.output + main.annotationProcessorPath + test.annotationProcessorPath
            srcDir file('src/functionalTest/java')
        }
        resources.srcDir file('src/functionalTest/resources')
    }
    smokeTest {
        java {
            compileClasspath += main.output + test.output + test.compileClasspath
            runtimeClasspath += main.output + test.output + test.runtimeClasspath
            annotationProcessorPath += main.output + test.output + main.annotationProcessorPath + test.annotationProcessorPath
            srcDir file('src/smokeTest/java')
        }
        resources.srcDir file('src/smokeTest/resources')
    }
    integrationTest {
        java {
            compileClasspath += main.output + test.output + test.compileClasspath
            runtimeClasspath += main.output + test.output + test.runtimeClasspath
            annotationProcessorPath += main.output + test.output + main.annotationProcessorPath + test.annotationProcessorPath
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}
checkstyle {
    maxWarnings = 0
    toolVersion = '10.14.2'
    getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

pmdIntegrationTest {
    maxFailures = 434
}
pmdTest {
    maxFailures = 381
}
pmdMain {
    maxFailures = 1139
}
pmd {
    maxFailures = 0
    toolVersion = "6.55.0"
    sourceSets = [sourceSets.main, sourceSets.test, sourceSets.integrationTest,
                  sourceSets.functionalTest, sourceSets.smokeTest]
    reportsDir = file("$project.buildDir/reports/pmd")
    // https://github.com/pmd/pmd/issues/876
    ruleSets = []
    ruleSetFiles = files("config/pmd/ruleset.xml")
}

test {
    if (System.getenv('BUILD_ID')) {
        failFast = true
    }
}

task integration(type: Test) {
    useJUnitPlatform()
    description = 'Runs integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    // This forces integration tests to always run if this task is run.
    outputs.upToDateWhen { false }
    if (System.getenv('BUILD_ID')) {
        failFast = true
    }
}

task testSuite(dependsOn: [test, integration]) {
    description = 'Runs both unit tests and integration tests.'
    group = 'verification'

    // This forces the test suite to always run if this task is run.
    outputs.upToDateWhen { false }
}

task smoke(type: Test) {
    useJUnitPlatform()
    description = "Runs Smoke Tests"
    testClassesDirs = sourceSets.smokeTest.output.classesDirs
    classpath = sourceSets.smokeTest.runtimeClasspath
}

task functional(type: Test) {
    useJUnitPlatform()
    description = "Runs functional tests"
    group = "Verification"
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}

jacocoTestReport {
    executionData(test, integration)
    reports {
        xml.required = true
        csv.required = false
        html.required = true
    }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 0-10 fails the build, anything greater and it doesn't fail the build
    failBuildOnCVSS = 10
    suppressionFile = 'config/owasp/suppressions.xml'

    analyzers {
        // Disable scanning of .NET related binaries
        assemblyEnabled = false
    }
    skipConfigurations = [
            "checkstyle",
            "compileOnly",
            "pmd",
            "integrationTest",
            "functionalTest",
            "test",
            "smokeTest",
            "contractTestRuntimeClasspath",
            "contractTestCompileClasspath"
    ]
}

project.tasks['check'].dependsOn integration
// Makes the whole test suite not run by default.
check.dependsOn -= testSuite

task runAllStyleChecks {
    dependsOn 'checkstyleMain'
    dependsOn 'checkstyleTest'
    dependsOn 'checkstyleIntegrationTest'
    dependsOn 'checkstyleSmokeTest'
    dependsOn 'checkstyleFunctionalTest'

    dependsOn 'pmdMain'
    dependsOn 'pmdTest'
    dependsOn 'pmdIntegrationTest'
    dependsOn 'pmdSmokeTest'
    dependsOn 'pmdFunctionalTest'
}

// Gradle 7.x issue, workaround from: https://github.com/gradle/gradle/issues/17236#issuecomment-894768083
rootProject.tasks.named("processSmokeTestResources") {
    duplicatesStrategy = 'include'
}
rootProject.tasks.named("processFunctionalTestResources") {
    duplicatesStrategy = 'include'
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}
